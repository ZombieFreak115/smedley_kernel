function_specifier = "inline" | "virtual" | "explicit"

nested_name_specifier = class_or_namespace_name "::" [nested_name_specifier] |
                        class_or_namespace_name "::" "template" nested_name_specifier

class_name = identifier | template_id
enum_name = identifier
typedef_name = identifier

type_name = class_name | enum_name | typedef_name

template_name = identifier
template_argument = assignment_expression | type_id | id_expression
template_argument_list = template_argument |
                         template_argument_list "," template_argument
template_id = template_name "<" [template_argument_list] ">"

simple_type_specifier = ["::"] [nested_name_specifier] type_name |
                        ["::"] nested_name_specifier "template" template_id |
                        "char" |
                        "wchar_t" |
                        "bool" |
                        "short" |
                        "int" |
                        "long" |
                        "signed" |
                        "unsigned" |
                        "float" |
                        "double" |
                        "void"

elaborated_type_specifier = class_key ["::"] [nested_name_specifier] identifier |
                            "enum" ["::"] [nested_name_specifier] identifier |
                            "typename" ["::"] nested_name_specifier identifier |
                            "typename" ["::"] nested_name_specifier ["template"] template_id

cv_qualifier = "const" | "volatile"

# class_specifier and enum_specifier currently removed for simplicity.
type_specifier = simple_type_specifier | elaborated_type_specifier | cv_qualifier

storage_class_specifier = "auto" | "register" | "static" | "extern" | "mutable"

decl_specifier = function_specifier | type_specifier | storage_class_specifier | "friend" | "typedef"
decl_specifier_seq = [decl_specifier_seq] decl_specifier

direct_declarator = declarator_id |
                    direct_declarator "(" parameter_declaration_clause ")" [cv_qualifier_seq] [exception_specification]
declarator = direct_declarator | ptr_operator declarator